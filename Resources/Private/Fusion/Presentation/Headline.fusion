prototype(Neos.Demo:Presentation.Headline) < prototype(Neos.Fusion:Component) {
    // This is used for the living styleguide (Monocle)
    // Read more about this in the README.md
    @styleguide {
        container = ${'<div class="prose">' + value + '</div>'}
        props {
            tagName = 'h1'
            tagStyle = ''
            class = ''
            content = 'I am a headline'
        }
    }

    tagName = 'h2'
    tagStyle = null
    class = null
    content = null

    @private {
        classByTag = Neos.Fusion:Match {
            @subject = ${props.tagStyle || props.tagName}
            @default = 'text-lg'
            h1 = 'text-5xl'
            h2 = 'text-4xl'
            h3 = 'text-3xl'
            h4 = 'text-2xl'
            h5 = 'text-xl'
        }
    }

    renderer = Neos.Fusion:Tag {
        tagName = ${props.tagName}
        attributes.class = ${props.class || private.classByTag}
        content = ${props.content}
    }
}
