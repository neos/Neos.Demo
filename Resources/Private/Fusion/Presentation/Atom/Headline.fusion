prototype(Neos.Demo:Atom.Headline) < prototype(Neos.Fusion:Component) {
    // This is used for the living styleguide (Monocle)
    // Read more about this in the README.md
    @styleguide {
        props {
            tagName = 'h1'
            tagStyle = ''
            class = ''
            content = 'I am a headline'
        }
    }

    tagName = 'h2'
    tagStyle = null
    class = null
    content = null

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        _class = Neos.Fusion:Match {
            @subject = ${props.tagStyle || props.tagName}
            @default = 'text-lg'
            h1 = 'text-5xl'
            h2= 'text-4xl'
            h3 = 'text-3xl'
            h4 = 'text-2xl'
            h5 = 'text-xl'
        }

        renderer = Neos.Fusion:Tag {
            tagName = ${props.tagName}
            attributes.class = ${props.class || props._class}
            content = ${props.content}
        }
    }
}
