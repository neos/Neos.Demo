##
# "Carousel" element
#
prototype(Site:Carousel) {
	carouselItems = ContentCollection {
		nodePath = 'carouselItems'
		content.iterationName = 'carouselItemsIteration'
		attributes.class = 'carousel-inner'
	}

	carouselItemArray = ${q(node).children('carouselItems').children('[instanceof Neos.NodeTypes:Image]')}

	// Enhance image prototype for the carousel
	prototype(Neos.NodeTypes:Image) {
		// Render images in the carousel with a special template.
		templatePath = 'resource://Neos.Demo/Private/Templates/FusionObjects/CarouselItem.html'

		attributes.class = ${'item' + (carouselItemsIteration.isFirst ? ' active' : '')}

		// We want to use the item iterator in the template so we have to store it in ts.
		iteration = ${carouselItemsIteration}
	}
}
