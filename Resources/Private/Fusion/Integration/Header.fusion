##
# The header section is a globally cached fragment that is used on every page.
# To make global caching possible the renderer is separated and instance on the root level.
# The component just renders this globally defined instance.
# This is necessary as the fusion path is part of the cache identifier and would include
# the current document if it's included directly without the global instance.
#
prototype(Neos.Demo:Integration.Header) < prototype(Neos.Fusion:Renderer) {
    renderPath = '/header'
}

prototype(Neos.Demo:Integration.Header.Renderer) < prototype(Neos.Demo:Presentation.Header) {
    homeUri = Neos.Neos:NodeUri {
        node = ${site}
    }

    menuItems = Neos.Fusion:Map {
        items = Neos.Neos:MenuItems {
            maximumLevels = 2
        }
        itemRenderer = Neos.Fusion:DataStructure {
            label = ${item.label}
            href = Neos.Neos:NodeUri {
                node = ${item.node}
            }
            subItems = Neos.Fusion:Map {
                items = ${item.subItems}
                itemRenderer = Neos.Fusion:DataStructure {
                    label = ${item.label}
                    href = Neos.Neos:NodeUri {
                        node = ${item.node}
                    }
                }
            }
        }
    }

    currentLanguage = ${site.context.dimensions.language[0]}

    renderer.@process.convertUris = Neos.Neos:ConvertUris {
        forceConversion = true
    }

    @cache {
        mode = 'cached'
        entryIdentifier {
            static = 'header'
            site = ${site}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', site)}
        }
    }
}

header = Neos.Demo:Integration.Header.Renderer
